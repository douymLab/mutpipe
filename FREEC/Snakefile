import socket
donor_sample_IDs, =glob_wildcards("path/to/BQSRTumorBamFolder/{sample}.tumor.bam")
# a pseudo-rule that collects the target files
rule all:
	input:
		expand("config/{sample}.config",sample=donor_sample_IDs),
		expand("result/{sample}.tumor.bam_CNVs",sample=donor_sample_IDs),
		expand("result/{sample}.tumor.bam_CNVs.p.value.txt",sample=donor_sample_IDs),
		expand("result/{sample}.tumor.bam_ratio.txt.png",sample=donor_sample_IDs),
		expand("result/{sample}.tumor.mainchr.bam_CNVs.p.value.txt",sample=donor_sample_IDs),
		expand("result/{sample}_median_ratio.txt",sample=donor_sample_IDs),
rule config:
	input:
		fai="resources/Homo_sapiens_assembly38.fasta.fai",
		tumor="path/to/BQSRTumorBamFolder/{sample}.tumor.bam",
		normal="path/to/BQSRTumorBamFolder/{sample}.normal.bam"
	output:
		config="config/{sample}.config"
	params:
		outputdir="result"
	shell:
		"""
		python tools/gen_freec_config.py -fai {input.fai} -tumor {input.tumor} -normal {input.normal} -dir {params.outputdir} -config {output.config}
		"""
rule freec:
	input: 
		conf="config/{sample}.config",
		sample="path/to/BQSRTumorBamFolder/{sample}.tumor.bam",
		control="path/to/BQSRTumorBamFolder/{sample}.normal.bam"
	output:
		cnv="result/{sample}.tumor.bam_CNVs",
		ratio="result/{sample}.tumor.bam_ratio.txt",
		bam_info="result/{sample}.tumor.bam_info.txt",
	shell:
		"tools/FREEC-11.6/src/freec -conf {input.conf} -sample {input.sample} -control {input.control}"
rule assessSignificance:
	input:
		cnv="result/{sample}.tumor.bam_CNVs",
		ratio="result/{sample}.tumor.bam_ratio.txt"
	output:
		pvalue="result/{sample}.tumor.bam_CNVs.p.value.txt"
	shell:
		"""
		cat tools/FREEC-11.6/scripts/assess_significance.R | R --slave --args {input.cnv} {input.ratio}
		"""
rule makeGraph:
	input:
		ratio="result/{sample}.tumor.bam_ratio.txt"
	output:
		png="result/{sample}.tumor.bam_ratio.txt.png"
	shell:
		"""
		cat tools/FREEC-11.6/scripts/makeGraph.R | R --slave --args 2 {input.ratio}
		"""
rule filter_main_chr:
        input:
                bam_CNVs="result/{sample}.tumor.bam_CNVs.p.value.txt",
                bam_ratio="result/{sample}.tumor.bam_ratio.txt",
        output:
                bam_mc_CNVs="result/{sample}.tumor.mainchr.bam_CNVs.p.value.txt",
                bam_mc_ratio="result/{sample}.tumor.mainchr.bam_ratio.txt",
        shell:
                """
                awk -F "\\t" '{{if(length($1)<=2 && $1!="M") print}}' {input.bam_CNVs} > {output.bam_mc_CNVs}
                awk -F "\\t" '{{if(length($1)<=2 && $1!="M") print}}' {input.bam_ratio} > {output.bam_mc_ratio}
                """
rule cal_median_ratio:
	input:
		bam_info="result/{sample}.tumor.bam_info.txt",
		bam_CNVs="result/{sample}.tumor.mainchr.bam_CNVs.p.value.txt",
		bam_ratio="result/{sample}.tumor.mainchr.bam_ratio.txt",
	output:
		median_ratio="result/{sample}_median_ratio.txt"
	params:
		sample="{sample}"
	shell:
		"""
		sh tools/freec_cal_median_ratio.sh {params.sample} {input.bam_info} {input.bam_CNVs} {input.bam_ratio} {output.median_ratio}
		"""
